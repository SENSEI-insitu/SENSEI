#
# Before calling this, ensure that the following variables are set:
#
# SVTK_GLOB_LIBS - a path containing a wildcard suitable for globbing
# SVTK_INSTALLED_HEADERS - the path containing all headers for the framework
# CMAKE_FRAMEWORK_INSTALL_PREFIX - the path in which to make svtk.framework
#

# Make sure SVTK is installed.
if (NOT EXISTS "@SVTK_INSTALLED_HEADERS@")
  message(FATAL_ERROR
          "SVTK not installed. Build the source then build the 'install' target.")
endif()

# Ensure a framework path is set
if (NOT EXISTS "@CMAKE_FRAMEWORK_INSTALL_PREFIX@")
  message(FATAL_ERROR
          "Please specify a CMAKE_FRAMEWORK_INSTALL_PREFIX.")
endif()

# Glob all input libs
file(GLOB SVTK_LIB_GLOB
  @SVTK_GLOB_LIBS@
)

# Set framework output path
set(FRAMEWORK_PATH "@CMAKE_FRAMEWORK_INSTALL_PREFIX@/svtk.framework")

# Do these individually, as we don't want them piped
execute_process(COMMAND mkdir -p "${FRAMEWORK_PATH}")
execute_process(COMMAND mkdir -p "${FRAMEWORK_PATH}/Headers")
execute_process(COMMAND cp -r "@SVTK_INSTALLED_HEADERS@/"
                              "${FRAMEWORK_PATH}/Headers/.")
execute_process(COMMAND libtool -static -o
                      "${FRAMEWORK_PATH}/svtk_lib"
                      ${SVTK_LIB_GLOB})
execute_process(COMMAND lipo -create "${FRAMEWORK_PATH}/svtk_lib"
                             -output "${FRAMEWORK_PATH}/svtk")
execute_process(COMMAND rm "${FRAMEWORK_PATH}/svtk_lib")

message(STATUS "Created ${FRAMEWORK_PATH}")