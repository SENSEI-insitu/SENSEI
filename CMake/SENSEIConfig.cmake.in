set(SENSEI_VERSION "@SENSEI_VERSION@")
set(SENSEI_VERSION_MAJOR "@SENSEI_VERSION_MAJOR@")
set(SENSEI_VERSION_MINOR "@SENSEI_VERSION_MINOR@")
set(SENSEI_VERSION_PATCH "@SENSEI_VERSION_PATCH@")
set(SENSEI_VERSION_DEVEL "@SENSEI_VERSION_DEVEL@")

include(CMakeFindDependencyMacro)

if (NOT SENSEI_DIR)
  # NOTE: this is for the ECP spack based regression testing.
  set(SENSEI_DIR "${CMAKE_CURRENT_LIST_DIR}/../../")
endif ()

list(APPEND CMAKE_MODULE_PATH "${SENSEI_DIR}")

list(APPEND CMAKE_PREFIX_PATH "${CMAKE_CURRENT_LIST_DIR}/svtk-9.0")
find_package(svtk REQUIRED)

set(SENSEI_LIB_TYPE STATIC)
if (@BUILD_SHARED_LIBS@)
  set(SENSEI_LIB_TYPE SHARED)
endif()

if (@BUILD_STATIC_EXECS@)
  set(BUILD_SHARED_LIBS OFF FORCE)
  set(CMAKE_FIND_LIBRARY_SUFFIXES ${CMAKE_STATIC_LIBRARY_SUFFIX})
  set(LINK_SEARCH_START_STATIC TRUE)
  set(LINK_SEARCH_END_STATIC TRUE)
endif()

set(ENABLE_SENSEI @ENABLE_SENSEI@)
set(ENABLE_PYTHON @ENABLE_PYTHON@)
set(ENABLE_CATALYST @ENABLE_CATALYST@)
set(ENABLE_CATALYST_PYTHON @ENABLE_CATALYST_PYTHON@)
set(ENABLE_LIBSIM @ENABLE_LIBSIM@)
set(ENABLE_ADIOS1 @ENABLE_ADIOS1@)
set(ENABLE_ADIOS2 @ENABLE_ADIOS2@)
set(ENABLE_HDF5 @ENABLE_HDF5@)
set(ENABLE_CONDUIT @ENABLE_CONDUIT@)
set(ENABLE_OSPRAY @ENABLE_OSPRAY@)
set(ENABLE_VTK_CORE @ENABLE_VTK_CORE@)
set(ENABLE_VTK_ACCELERATORS @ENABLE_VTK_ACCELERATORS@)
set(ENABLE_VTK_MPI @ENABLE_VTK_MPI@)
set(ENABLE_VTK_IO @ENABLE_VTK_IO@)
set(ENABLE_VTKM @ENABLE_VTKM@)
set(ENABLE_VTKM_RENDERING @ENABLE_VTKM_RENDERING@)
set(ENABLE_ASCENT @ENABLE_ASCENT@)

include(thread)
include(sMPI)
include(sSVTK)
include(pugixml)
include(sDIY)

if (ENABLE_CATALYST)
  if (NOT ParaView_DIR)
    set(ParaView_DIR "@ParaView_DIR@")
  endif()
  find_dependency(ParaView)
elseif (ENABLE_VTK_CORE)
  if (NOT VTK_DIR)
    set(VTK_DIR "@VTK_DIR@")
  endif()
  find_dependency(VTK)
endif()

if (ENABLE_VTK_CORE)
  include(sVTK)
endif ()

if (ENABLE_VTKM)
  include(sVTKm)
endif()

if (ENABLE_LIBSIM)
  include(sLibsim)
endif()

if (ENABLE_ADIOS1)
  include(sADIOS1)
endif()

if (ENABLE_ADIOS2)
  if(NOT ADIOS2_DIR)
    set(ADIOS2_DIR "@ADIOS2_DIR@")
  endif()
  find_dependency(ADIOS2)
  include(sADIOS2)
endif()

if (ENABLE_HDF5)
  include(sHDF5)
endif()

if (ENABLE_CONDUIT)
  if(NOT CONDUIT_DIR)
    set(CONDUIT_DIR "@CONDUIT_DIR@")
  endif()
  find_dependency(Conduit NO_DEFAULT_PATH PATHS ${CONDUIT_DIR})
  include(sConduit)
endif()

if(ENABLE_ASCENT)
  if(NOT ASCENT_DIR)
    set(ASCENT_DIR "@ASCENT_DIR@")
  endif()
  find_dependency(Ascent NO_DEFAULT_PATH PATHS ${ASCENT_DIR})
  include(sAscent)
endif()

include(senseiCore)

if (ENABLE_PYTHON)
  set(SENSEI_PYTHON_DIR "${CMAKE_CURRENT_LIST_DIR}/../../../@SENSEI_PYTHON_DIR@")

  include(sPython)

  add_library(sPythonAnalysis SHARED IMPORTED)
  set_property(TARGET sPythonAnalysis
    PROPERTY IMPORTED_LOCATION
    "${SENSEI_PYTHON_DIR}/_PythonAnalysis.so")
  if (APPLE)
    set_property(TARGET sPythonAnalysis
    PROPERTY IMPORTED_SONAME "@rpath/_PythonAnalysis.so")
  endif()
  add_dependencies(sPythonAnalysis PythonAnalysis)

endif()

include(sensei)
